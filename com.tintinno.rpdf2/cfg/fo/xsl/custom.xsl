<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
                xmlns:xs="http://www.w3.org/2001/XMLSchema"
                xmlns:fo="http://www.w3.org/1999/XSL/Format"
                xmlns:fox="http://xmlgraphics.apache.org/fop/extensions"
				version="2.0">
  
  <!-- apply the template for concept/title -->
  <xsl:template match="/concept/title">
    <fo:block xsl:use-attribute-sets="my-name">
      <xsl:apply-templates/>
    </fo:block>
  </xsl:template>

  <!-- apply the template for info section -->
  <xsl:template match="//section[@id = 'info']">
    <fo:block xsl:use-attribute-sets="center" margin-top="5px">
      <xsl:text>Phone: </xsl:text><xsl:value-of select=".//p[@id='phone']"/>
      <xsl:text>&#160;&#160;~&#160;&#160;</xsl:text>
      <xsl:text>Email: </xsl:text><xsl:value-of select=".//p[@id='email']"/>
      <xsl:text>&#160;&#160;~&#160;&#160;</xsl:text>
      <!-- insert a link to blog -->
      <xsl:variable name="bloglink" select=".//p[@id='blog']/text()"/>
      <fo:basic-link color="#3366cc" external-destination="url({$bloglink})">
         <xsl:text>Blog</xsl:text>
      </fo:basic-link>
      <xsl:text>&#160;&#160;~&#160;&#160;</xsl:text>
      <!-- insert a link to github -->
      <xsl:variable name="githublink" select=".//p[@id='github']/text()"/>
      <fo:basic-link color="#3366cc" external-destination="url({$githublink})">
         <xsl:text>Github</xsl:text>
      </fo:basic-link>
    </fo:block>
  </xsl:template>
  
  <!-- apply the template for section/title -->
  <xsl:template match="//section/title">
    <fo:block xsl:use-attribute-sets="section-heading">
    <!-- pilcrows are a ancient and respectable typographical mark 
      <xsl:text>&#182;&#160;&#160;&#160;</xsl:text>
      -->
        <xsl:value-of select="text()"/>
    </fo:block>
  </xsl:template>
  
  <!-- apply the template for photo -->
  <xsl:template match="//section[@id = 'photo']">
    <fo:block xsl:use-attribute-sets="photo-attrs">
      <xsl:apply-templates/>
    </fo:block>
  </xsl:template>

  <!-- edit footer to insert link to github -->
  <xsl:template name="insertBodyOddFooter"> 
    <fo:static-content flow-name="odd-body-footer"> 
      <fo:block xsl:use-attribute-sets="my-footer"> 
        <xsl:text>Generated by https://github.com/tintinno/plugins</xsl:text>
      </fo:block> 
    </fo:static-content> 
  </xsl:template> 

  <!-- edit header to insert silly hidden message -->
  <xsl:template name="insertBodyOddHeader"> 
    <fo:static-content flow-name="odd-body-header"> 
      <fo:block xsl:use-attribute-sets="my-header"> 
        <xsl:text>You should hire me.</xsl:text>
      </fo:block> 
    </fo:static-content> 
  </xsl:template> 

  <!-- template for table bodies -->
  <xsl:template name="table-body">
	<fo:table-body xsl:use-attribute-sets="no-margin-no-pad">
	<xsl:for-each select="li">
	  <fo:table-row xsl:use-attribute-sets="no-margin-no-pad">
	   <fo:table-cell xsl:use-attribute-sets="no-margin-no-pad">
		<fo:block>
		 <xsl:value-of select="substring-before(text(),'|')"/>
		</fo:block>
	   </fo:table-cell>
	   <fo:table-cell xsl:use-attribute-sets="no-margin-no-pad">
		<fo:block>
         <xsl:choose>
           <xsl:when test="contains( substring-before( substring-after(text(),'|') , '|'), ','  )">
             <!-- The value contain a link after the comma -->
             <xsl:variable name="linktext" select="substring-before( substring-before( substring-after(text(),'|') , '|'), ','  )"/>
             <xsl:variable name="link" select="substring-after( substring-before( substring-after(text(),'|') , '|'), ','  )"/>
             <fo:basic-link color="#3366cc" external-destination="url({$link})">
                <xsl:value-of select="$linktext"/>
             </fo:basic-link>
           </xsl:when>
           <xsl:otherwise>
             <!-- The value does not contain a link after the comma -->
		     <xsl:value-of select="substring-before( substring-after(text(),'|') , '|')"/>
           </xsl:otherwise>
         </xsl:choose>
		</fo:block>
	   </fo:table-cell>
	   <fo:table-cell xsl:use-attribute-sets="no-margin-no-pad">
		<fo:block>
		 <xsl:value-of select="substring-after( substring-after(text(),'|') , '|')"/>
		</fo:block>
	   </fo:table-cell>
	  </fo:table-row>
	</xsl:for-each>
	</fo:table-body>
  </xsl:template>
  
  <!-- template for skills -->
  <xsl:template name="skillz-table">
	<fo:table-body>
	<xsl:for-each select="li">
	  <fo:table-row xsl:use-attribute-sets="no-margin-no-pad">
	   <fo:table-cell xsl:use-attribute-sets="no-margin-no-pad">
		<fo:block>
		 <xsl:value-of select="substring-before(text(),'|')"/>
		</fo:block>
	   </fo:table-cell>
	   <fo:table-cell xsl:use-attribute-sets="no-margin-no-pad">
		<fo:block>
		  <xsl:value-of select="substring-before( substring-after(text(),'|') , '|')"/>
		</fo:block>
	   </fo:table-cell>
	   <fo:table-cell xsl:use-attribute-sets="no-margin-no-pad">
		<fo:block>
		 <xsl:value-of select="substring-after( substring-after(text(),'|') , '|')"/>
		</fo:block>
	   </fo:table-cell>
	  </fo:table-row>
	</xsl:for-each>
	</fo:table-body>
  </xsl:template>
  
  <!-- edit tables for 'writing samples' section -->
  <xsl:template match="//section[@id='skills']/ul">
    <fo:table xsl:use-attribute-sets="no-margin-no-pad">
	  <xsl:call-template name="skillz-table"/>
    </fo:table> 
  </xsl:template>
  
  <!-- edit tables for 'writing samples' section -->
  <xsl:template match="//section[@id='writingsamples']/ul">
    <fo:table xsl:use-attribute-sets="no-margin-no-pad">
	<fo:table-header>
	 <fo:table-row xsl:use-attribute-sets="no-margin-no-pad">
		<fo:table-cell xsl:use-attribute-sets="no-margin-no-pad">
			<fo:block font-weight="bold">Product</fo:block>
		</fo:table-cell>
		<fo:table-cell xsl:use-attribute-sets="no-margin-no-pad">
			<fo:block font-weight="bold">Document or Section</fo:block>
		</fo:table-cell>
		<fo:table-cell xsl:use-attribute-sets="no-margin-no-pad">
			<fo:block font-weight="bold">Year</fo:block>
		</fo:table-cell>
	 </fo:table-row>
	</fo:table-header>
	<xsl:call-template name="table-body"/>
    </fo:table> 
  </xsl:template>

  <!-- edit tables for 'independent coursework' section -->
  <xsl:template match="//section[@id='coursework']/ul">
    <fo:table xsl:use-attribute-sets="no-margin-no-pad">
	<fo:table-header>
	 <fo:table-row xsl:use-attribute-sets="no-margin-no-pad">
		<fo:table-cell xsl:use-attribute-sets="no-margin-no-pad">
			<fo:block font-weight="bold">Class</fo:block>
		</fo:table-cell>
		<fo:table-cell xsl:use-attribute-sets="no-margin-no-pad">
			<fo:block font-weight="bold">Institution</fo:block>
		</fo:table-cell>
		<fo:table-cell xsl:use-attribute-sets="no-margin-no-pad">
			<fo:block font-weight="bold">Semester</fo:block>
		</fo:table-cell>
	 </fo:table-row>
	</fo:table-header>
	<xsl:call-template name="table-body"/>
    </fo:table> 
  </xsl:template>

  <!-- edit tables for 'education' section -->
  <xsl:template match="//section[@id='education']/ul">
    <fo:table xsl:use-attribute-sets="no-margin-no-pad">
	<fo:table-header>
	 <fo:table-row xsl:use-attribute-sets="no-margin-no-pad">
		<fo:table-cell xsl:use-attribute-sets="no-margin-no-pad">
			<fo:block font-weight="bold">Degree</fo:block>
		</fo:table-cell>
		<fo:table-cell xsl:use-attribute-sets="no-margin-no-pad">
			<fo:block font-weight="bold">Institution</fo:block>
		</fo:table-cell>
		<fo:table-cell xsl:use-attribute-sets="no-margin-no-pad">
			<fo:block font-weight="bold">Years</fo:block>
		</fo:table-cell>
	 </fo:table-row>
	</fo:table-header>
	<xsl:call-template name="table-body"/>
    </fo:table> 
  </xsl:template>

  <!-- edit tables for 'experience' section -->
  <xsl:template match="//section[@id='experience']/p">
    <fo:table xsl:use-attribute-sets="my-experience">
	<fo:table-column column-width="30%"/>
	<fo:table-column column-width="70%"/>
	 <fo:table-body>
     <xsl:for-each select=".">
      <fo:table-row>
		<fo:table-cell xsl:use-attribute-sets="no-margin-no-pad">
		 <fo:block font-weight="bold">
		  <xsl:value-of select="substring-before(text(),'|')"/>
		 </fo:block>
		 <fo:block>
		   <xsl:value-of select="substring-before( substring-after(text(),'|') , '|')"/>
		 </fo:block>
		 <fo:block>
		   <xsl:value-of select="substring-after( substring-after(text(),'|') , '|')"/>
		 </fo:block>
		</fo:table-cell> 
		<fo:table-cell xsl:use-attribute-sets="no-margin-no-pad">
		  <xsl:for-each select="./following-sibling::ul[1]/li">
			<fo:list-block>
			 <fo:list-item>
			  <fo:list-item-label end-indent="label-end()">
			   <fo:block text-align="right">
				<fo:inline>â€¢</fo:inline>
			   </fo:block>
			  </fo:list-item-label>
			  <fo:list-item-body start-indent="body-start()">
			   <fo:block>
				  <xsl:value-of select="."/>
				</fo:block>
			  </fo:list-item-body>
			 </fo:list-item>
			</fo:list-block>
		  </xsl:for-each>
		</fo:table-cell>
      </fo:table-row> 
     <!-- if we have another item in our list, add an empty row for space -->
     <xsl:if test="./following-sibling::p">
      <fo:table-row padding="0" margin="0">
       <fo:table-cell>
         <fo:block>
           <xsl:text>&#160;</xsl:text>
         </fo:block>
       </fo:table-cell>
      </fo:table-row> 
     </xsl:if>
     <!-- end if -->
      </xsl:for-each>
	 </fo:table-body>
    </fo:table> 
  </xsl:template>
  
  <!-- insert a photo -->
  <xsl:template match="//section[@id='photo']/p/image">
   <xsl:variable name="href" select="//section[@id='photo']/p/image/@href"/>
    <fo:block-container absolute-position="absolute">
    <fo:block 
        text-align="right"
        margin-top="13mm"
      >
      <fo:external-graphic 
            content-width="37mm" 
            height="37mm" 
            src="/tmp/ditaot/myphoto.png"/>
      </fo:block>
    </fo:block-container>
  </xsl:template>

  
  <!-- and let's get rid of the old ul -->
  <xsl:template match="//section[@id='experience']/ul"/>

</xsl:stylesheet>
